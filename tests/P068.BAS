10 PRINT "PROGRAM FILE 68: EXCEPTION - SUBSCRIPT TOO LARGE FOR"
15 PRINT "        ONE-DIMENSIONAL ARRAY, WITH DIM AND OPTION BASE 1."
20 PRINT "    ANSI STANDARD 6.5, 15.2, 15.4"
30 PRINT
40 PRINT "SECTION 68.1: EXCEPTION - SUBSCRIPT TOO LARGE FOR"
50 PRINT "        ONE-DIMENSIONAL ARRAY, WITH DIM AND OPTION BASE 1."
60 PRINT
70 PRINT "THE OBJECT OF THIS TEST IS TO SEE WHETHER THE PROCESSOR"
80 PRINT "RECOGNIZES A FATAL EXCEPTION WHEN A SUBSCRIPT EXCEEDS"
90 PRINT "THE EXPLICIT UPPER BOUND OF AN ARRAY, WITH AN OPTION"
100 PRINT "STATEMENT PRESENT."
110 PRINT
120 PRINT "TO PASS THIS TEST:"
130 PRINT
140 PRINT "    1) A MESSAGE IDENTIFYING THE EXCEPTION MUST BE"
150 PRINT "       DISPLAYED, AND"
160 PRINT
170 PRINT "    2) THE PROGRAM MUST TERMINATE UPON USE OF THE ILLEGAL"
180 PRINT "       SUBSCRIPT."
190 PRINT
200 PRINT "                               BEGIN TEST."
210 OPTION BASE 1
220 DIM A(7)
230 PRINT
240 FOR I=5 TO 9
250   PRINT "ABOUT TO ASSIGN TO A(";I;"). ";
260   IF I<8 THEN 290
270   PRINT "*** EXCEPTION SHOULD OCCUR NOW ***"
280   GOTO 300
290   PRINT "ASSIGNMENT SHOULD BE OK."
300   LET A(I) = 20 - I
310   PRINT "HAVE ASSIGNED ";20-I;" TO A(";I;")."
320   IF I<8 THEN 340
330   PRINT "ASSIGNMENT TO SUBSCRIPT > 7 - *** TEST FAILS ***"
340   PRINT
350 NEXT I
360 PRINT
370 PRINT "                                END TEST."
380 PRINT
390 PRINT "END PROGRAM 68"
400 END
